plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.graalvm.buildtools.native' version '0.10.2'
    id 'com.google.osdetector' version '1.7.3'
    id "application"
}

project.ext.lwjglVersion = "3.3.4"

group = 'com.github.qpcrummer'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "TarsosDSP repository"
        url = "https://mvn.0110.be/releases"
    }
}

def getCurrentPlatform() {
    def os = osdetector.os
    def lwjglOS = os == 'linux' ? 'linux' :
            os == 'osx' ? 'macos' :
                    os == 'windows' ? 'windows' :
                            'unknown'
    if (lwjglOS == 'unknown')
        throw new GradleException('Unsupported OS: ' + os)

    def arch = osdetector.arch
    //consider 64-bit architectures only
    def lwjglArch = arch == 'x86_64' ? '' :
            arch == 'aarch_64' ? 'arm64' :
                    'unknown'
    if (lwjglArch == 'unknown')
        throw new GradleException('Unsupported Architecture: ' + arch)

    return (lwjglArch == '' ? lwjglOS : (lwjglOS + '-' + lwjglArch))
}

ext {
    lwjglVersion = '3.3.4'

    //LWJGL modules used: minimal OpenGL
    lwjglModules = ['lwjgl', 'lwjgl-assimp', 'lwjgl-glfw', 'lwjgl-openal', 'lwjgl-opengl', 'lwjgl-stb']

    //mainClassName = 'com.github.qpcrummer.beatmaker.Main'
    currentPlatform = getCurrentPlatform()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    implementation 'org.slf4j:slf4j-api:2.1.0-alpha1'
    implementation 'org.slf4j:slf4j-jdk14:2.1.0-alpha1'
    implementation ('io.github.spair:imgui-java-app:1.86.11')
    implementation 'be.tarsos.dsp:core:2.5'
    implementation 'be.tarsos.dsp:jvm:2.5'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.github.oshi:oshi-core:6.6.1'
    implementation 'net.lingala.zip4j:zip4j:2.11.5'

    //get recommended dependency versions from the LWJGL BOM
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    //add LWJGL modules and the current OS's natives to the compile and runtime classpaths
    lwjglModules.each {
        implementation "org.lwjgl:$it"
        if (it != 'lwjgl-egl')  //lwjgl-egl has no native libraries
            runtimeOnly "org.lwjgl:$it::natives-$currentPlatform"
    }
}

task extractLwjglManifest(type: Copy) {
    from {
        configurations.compileClasspath.filter { it.name == "lwjgl-${lwjglVersion}.jar" }
                .collect { zipTree(it) }
    }
    include 'META-INF/MANIFEST.MF'
    into "$buildDir/tmp"
}

application {
    mainClassName = 'com.github.qpcrummer.beatmaker.Main'
}

graalvmNative {
    toolchainDetection = false
    binaries {
        main {
            imageName = "OpenLightsBeatMaker"
            mainClass = "com.github.qpcrummer.beatmaker.Main"
            debug = false
            verbose = false
            fallback = false

            buildArgs.add('--initialize-at-run-time=org.lwjgl')
            buildArgs.add('--initialize-at-run-time=io.imgui')
            buildArgs.add('--native-image-info')
            buildArgs.add('-march=compatibility')

            useFatJar = false
        }
    }
    metadataRepository {
        enabled = false
    }
}

test {
    useJUnitPlatform()
}

shadowJar {
    manifest {
        attributes(
                'Main-Class': 'com.github.qpcrummer.beatmaker.Main'
        )
    }
}